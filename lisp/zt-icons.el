(defcustom emacs-solo-enabled-icons
  '(dired eshell ibuffer)
  "List of Emacs Solo icon features that are enabled."
  :type '(set :tag "Enabled Emacs Solo icon features"
              (const :tag "Dired Icons" dired)
              (const :tag "Eshell Icons" eshell)
              (const :tag "Ibuffer Icons" ibuffer)
              (const :tag "Nerd Font Icons" nerd))
  :group 'emacs-solo)

;;; â”‚ EMACS-SOLO-FILE-ICONS
;;
;;  Here we set the icons to be used by other `emacs-solo' features,
;;  like `emacs-solo-dired-icons' and `emacs-solo-eshell-icons'
(use-package emacs-solo-file-icons
  :if emacs-solo-enabled-icons
  :straight nil
  :ensure nil
  :no-require t
  :defer t
  :init
  (let ((emoji-icons
         '(("el" . "ğŸ“œ")       ("rb" . "ğŸ’")       ("js" . "âš™ï¸")      ("ts" . "âš™ï¸")
           ("json" . "ğŸ—‚ï¸")     ("md" . "ğŸ“")       ("txt" . "ğŸ“")     ("html" . "ğŸŒ")
           ("css" . "ğŸ¨")      ("scss" . "ğŸ¨")     ("png" . "ğŸ–¼ï¸")     ("jpg" . "ğŸ–¼ï¸")
           ("jpeg" . "ğŸ–¼ï¸")     ("gif" . "ğŸ–¼ï¸")      ("svg" . "ğŸ–¼ï¸")     ("pdf" . "ğŸ“„")
           ("zip" . "ğŸ“¦")      ("tar" . "ğŸ“¦")      ("gz" . "ğŸ“¦")      ("bz2" . "ğŸ“¦")
           ("7z" . "ğŸ“¦")       ("org" . "ğŸ¦„")      ("sh" . "ğŸ’»")      ("c" . "ğŸ…²")
           ("h" . "ğŸ“˜")        ("cpp" . "ğŸ…²")      ("hpp" . "ğŸ“˜")     ("py" . "ğŸ")
           ("java" . "â˜•")    ("go" . "ğŸŒ")       ("rs" . "ğŸ’¨")      ("php" . "ğŸ˜")
           ("pl" . "ğŸ")       ("lua" . "ğŸ®")      ("ps1" . "ğŸ”§")     ("exe" . "âš¡")
           ("dll" . "ğŸ”Œ")      ("bat" . "âš¡")     ("yaml" . "âš™ï¸")    ("toml" . "âš™ï¸")
           ("ini" . "âš™ï¸")      ("csv" . "ğŸ“Š")      ("xls" . "ğŸ“Š")     ("xlsx" . "ğŸ“Š")
           ("sql" . "ğŸ—„ï¸")      ("log" . "ğŸ“")      ("apk" . "ğŸ“±")     ("dmg" . "ğŸ’»")
           ("iso" . "ğŸ’¿")      ("torrent" . "ğŸ§²")  ("bak" . "ğŸ—ƒï¸")     ("tmp" . "âš ï¸")
           ("desktop" . "ğŸ–¥ï¸")  ("md5" . "ğŸ”")      ("sha256" . "ğŸ”")  ("pem" . "ğŸ”")
           ("sqlite" . "ğŸ—„ï¸")   ("db" . "ğŸ—„ï¸")       ("gpg" . "ğŸ”")     ("hash" . "#ï¸âƒ£")
           ("mp3" . "ğŸ¶")      ("wav" . "ğŸ¶")      ("flac" . "ğŸ¶" )  ("mail" . "ğŸ“§")
           ("ogg" . "ğŸ¶")      ("m4a" . "ğŸ¶")      ("mp4" . "ğŸ¬")     ("avi" . "ğŸ¬")
           ("mov" . "ğŸ¬")      ("mkv" . "ğŸ¬")      ("webm" . "ğŸ¬")    ("flv" . "ğŸ¬")
           ("ico" . "ğŸ–¼ï¸")      ("ttf" . "ğŸ” ")      ("otf" . "ğŸ” ")     ("eot" . "ğŸ” ")
           ("woff" . "ğŸ” ")     ("woff2" . "ğŸ” ")    ("epub" . "ğŸ“š")    ("mobi" . "ğŸ“š")
           ("azw3" . "ğŸ“š")     ("fb2" . "ğŸ“š")      ("chm" . "ğŸ“š")     ("tex" . "ğŸ“š")
           ("bib" . "ğŸ“š")      ("apk" . "ğŸ“±")      ("rar" . "ğŸ“¦")     ("xz" . "ğŸ“¦")
           ("zst" . "ğŸ“¦")      ("tar.xz" . "ğŸ“¦")   ("tar.zst" . "ğŸ“¦") ("tar.gz" . "ğŸ“¦")
           ("tgz" . "ğŸ“¦")      ("bz2" . "ğŸ“¦")      ("mpg" . "ğŸ¬")     ("webp" . "ğŸ–¼ï¸")
           ("flv" . "ğŸ¬")      ("3gp" . "ğŸ¬")      ("ogv" . "ğŸ¬")     ("srt" . "ğŸ” ")
           ("vtt" . "ğŸ” ")      ("cue" . "ğŸ“€")      ("terminal" . "ğŸ’»") ("info" . "â„¹ï¸")
           ("direddir" . "ğŸ“") ("diredfile" . "ğŸ“„") ("wranch" . "ğŸ”§") ("news" . "ğŸ“°")))
        (nerd-icons
         '(("el" . "îŸ")       ("rb" . "î‘")       ("js" . "î˜Œ")      ("ts" . "î˜¨")
           ("json" . "î˜‹")     ("md" . "î˜‰")       ("txt" . "ïƒ¶")     ("html" . "î˜")
           ("css" . "î˜”")      ("scss" . "î˜ƒ")     ("png" . "î˜")     ("jpg" . "î˜")
           ("jpeg" . "î˜")     ("gif" . "î˜")      ("svg" . "î˜")     ("pdf" . "î™½")
           ("zip" . "ï‡†")      ("tar" . "î˜†")      ("gz" . "ï‡†")      ("bz2" . "ï‡†")
           ("7z" . "î˜†")       ("org" . "î˜³")      ("sh" . "î•")      ("c" . "î˜")
           ("h" . "î˜")        ("cpp" . "î˜")      ("hpp" . "î˜")     ("py" . "î˜†")
           ("java" . "îœ¸")    ("go" . "î˜¦")       ("rs" . "î¨")      ("php" . "î˜ˆ")
           ("pl" . "î©")       ("lua" . "î˜ ")      ("ps1" . "î•")     ("exe" . "îœ")
           ("dll" . "îœ")      ("bat" . "îœ")     ("yaml" . "î˜•")    ("toml" . "î˜•")
           ("ini" . "î˜•")      ("csv" . "î™Š")      ("xls" . "î™Š")     ("xlsx" . "î™Š")
           ("sql" . "îœ†")      ("log" . "ïƒ¶")      ("apk" . "îœ")     ("dmg" . "îœ‘")
           ("iso" . "îœ±")      ("torrent" . "îœ½")  ("bak" . "î™")     ("tmp" . "î™")
           ("desktop" . "îœ‘")  ("md5" . "îœ†")      ("sha256" . "îœ†")  ("pem" . "îœ†")
           ("sqlite" . "îœ†")   ("db" . "îœ†")       ("gpg" . "îœ†")     ("hash" . "ïŠ’")
           ("mp3" . "ï€")      ("wav" . "ï€")      ("flac" . "ï€" )   ("mail" . "î¬œ")
           ("ogg" . "ï€")      ("m4a" . "ï€")      ("mp4" . "ï€ˆ")     ("avi" . "ï€ˆ")
           ("mov" . "ï€ˆ")      ("mkv" . "ï€ˆ")      ("webm" . "ï€ˆ")    ("flv" . "ï€ˆ")
           ("ico" . "î˜")      ("ttf" . "ï€±")      ("otf" . "ï€±")     ("eot" . "ï€±")
           ("woff" . "ï€±")     ("woff2" . "ï€±")    ("epub" . "î˜")    ("mobi" . "î˜")
           ("azw3" . "î˜")     ("fb2" . "î˜")      ("chm" . "î˜")     ("tex" . "î˜€")
           ("bib" . "î˜€")      ("rar" . "ï‡†")     ("xz" . "ï‡†")
           ("zst" . "ï‡†")      ("tar.xz" . "ï‡†")   ("tar.zst" . "ï‡†") ("tar.gz" . "î˜†")
           ("tgz" . "ï‡†")      ("bz2" . "ï‡†")      ("mpg" . "ï€ˆ")     ("webp" . "î˜")
           ("flv" . "ï€ˆ")      ("3gp" . "ï€ˆ")      ("ogv" . "ï€ˆ")     ("srt" . "î˜Š")
           ("vtt" . "î˜Š")      ("cue" . "î™„")      ("terminal" . "î•") ("info" . "â„¹")
           ("direddir" . "î—¿") ("diredfile" . "î˜’") ("wranch" . "ï€“") ("news" . "ï‡ª"))))

    (defvar emacs-solo/file-icons
      (cond
       ;; If nerd icons are enabled, use them.
       ((memq 'nerd emacs-solo-enabled-icons)
        nerd-icons)
       ;; If on kitty terminal AND not using nerd icons, use blank icons
       ;; to prevent emoji rendering issues.
       ((string= (getenv "TERM") "xterm-kitty")
        (mapcar (lambda (p) (cons (car p) "")) emoji-icons))
       ;; Otherwise, use the default emoji icons.
       (t
        emoji-icons))
      "Icons for specific file extensions in Dired and Eshell.")))


;;; â”‚ EMACS-SOLO-DIRED-ICONS
;;
(use-package emacs-solo-dired-icons
  :if (memq 'dired emacs-solo-enabled-icons)
  :straight nil  
  :ensure nil
  :no-require t
  :defer t
  :init
  (defun emacs-solo/dired-icons-icon-for-file (file)
    (if (file-directory-p file)
        (assoc-default "direddir" emacs-solo/file-icons)
      (let* ((ext (file-name-extension file))
             (icon (and ext (assoc-default (downcase ext) emacs-solo/file-icons))))
        (or icon (assoc-default "diredfile" emacs-solo/file-icons)))))

  (defun emacs-solo/dired-icons-icons-regexp ()
    "Return a regexp that matches any icon we use."
    (let ((icons (mapcar #'cdr emacs-solo/file-icons)))
      (concat "^\\(" (regexp-opt (cons "ğŸ“" icons)) "\\) ")))

  (defun emacs-solo/dired-icons-add-icons ()
    "Add icons and suffixes as overlays to filenames in Dired buffer."
    (when (derived-mode-p 'dired-mode)
      (let ((inhibit-read-only t)
            (icon-regex (emacs-solo/dired-icons-icons-regexp)))
        (remove-overlays (point-min) (point-max) 'emacs-solo-dired-icon-overlay t)

        (save-excursion
          (goto-char (point-min))
          (while (not (eobp))
            (condition-case nil
                (when-let* ((file (dired-get-filename nil t)))
                  (dired-move-to-filename)
                  (let* ((beg (point))
                         (end (line-end-position))
                         (icon (emacs-solo/dired-icons-icon-for-file file))
                         (suffix
                          (cond
                           ((file-directory-p file)
                            (propertize "/" 'face 'dired-directory))
                           ((file-executable-p file)
                            (propertize "*" 'face '(:foreground "#79a8ff")))
                           (t ""))))
                    ;; Add icon before filename
                    (let ((ov1 (make-overlay beg beg)))
                      (overlay-put ov1 'before-string (concat icon " "))
                      (overlay-put ov1 'emacs-solo-dired-icon-overlay t))
                    ;; Add styled suffix after filename
                    (let ((ov2 (make-overlay end end)))
                      (overlay-put ov2 'after-string suffix)
                      (overlay-put ov2 'emacs-solo-dired-icon-overlay t))))
              (error nil))
            (forward-line 1))))))

  (add-hook 'dired-after-readin-hook #'emacs-solo/dired-icons-add-icons))


;;; â”‚ EMACS-SOLO-IBUFFER-ICONS
;;
(use-package emacs-solo-ibuffer-icons
  :if (memq 'ibuffer emacs-solo-enabled-icons)
  :ensure nil
  :straight nil  
  :no-require t
  :defer t
  :init
  (defun emacs-solo/ibuffer-icon-for-buffer (buf)
    "Return an icon for BUF: file-extension emoji if visiting a file,
otherwise mode-based emoji."
    (with-current-buffer buf
      (if-let* ((file (buffer-file-name)))
          ;; File-based icons
          (let* ((ext (file-name-extension file))
                 (icon (and ext (assoc-default (downcase ext) emacs-solo/file-icons))))
            (or icon (assoc-default "diredfile" emacs-solo/file-icons)))
        ;; Mode-based icons for non-file buffers
        (cond
         ((derived-mode-p 'dired-mode)  (assoc-default "direddir" emacs-solo/file-icons))
         ((derived-mode-p 'eshell-mode) (assoc-default "terminal" emacs-solo/file-icons))
         ((derived-mode-p 'org-mode)    (assoc-default "terminal" emacs-solo/file-icons))
         ((derived-mode-p 'shell-mode)  (assoc-default "terminal" emacs-solo/file-icons))
         ((derived-mode-p 'term-mode)   (assoc-default "terminal" emacs-solo/file-icons))
         ((derived-mode-p 'help-mode)   (assoc-default "info" emacs-solo/file-icons))
         ((derived-mode-p 'erc-mode)    (assoc-default "hash" emacs-solo/file-icons))
         ((derived-mode-p 'rcirc-mode)  (assoc-default "hash" emacs-solo/file-icons))
         ((derived-mode-p 'gnus-mode)   (assoc-default "mail" emacs-solo/file-icons))
         ((derived-mode-p 'newsticker-treeview-mode)   (assoc-default "news" emacs-solo/file-icons))
         (t                             (assoc-default "wranch" emacs-solo/file-icons))))))

  (define-ibuffer-column icon
    (:name " ")
    (emacs-solo/ibuffer-icon-for-buffer buffer))

  ;; Update ibuffer formats
  (setq ibuffer-formats
        '((mark modified read-only locked " "
                (icon 2 2 :left) " "
                (name 30 30 :left :elide) " "
                (size 9 -1 :right) " "
                (mode 16 16 :left :elide) " "
                filename-and-process))))


;;; â”‚ EMACS-SOLO-ESHELL-ICONS
;;
;; Inspired by: https://www.reddit.com/r/emacs/comments/xboh0y/how_to_put_icons_into_eshell_ls/
(use-package emacs-solo-eshell-icons
  :if (memq 'eshell emacs-solo-enabled-icons)
  :ensure nil
  :straight nil  
  :no-require t
  :defer t
  :init
  (defun emacs-solo/eshell-icons (file)
    "Return a cons of propertized display string and file metadata.
FILE is a list (NAME IS-DIR EXECUTABLE ...), like from `eshell/ls`.
The full list is like:
(FILENAME IS-DIR SIZE OWNER GROUP MOD-TIME ACCESS-TIME CHANGE-TIME
SIZE-LONG PERMS HARDLINKS INODE DEVICE).
"
    (let* ((filename (car file))
           (is-dir (eq (cadr file) t))
           (perms (nth 9 file))
           (is-exec (and perms (string-match-p "x" perms)))
           (ext (and (not is-dir) (file-name-extension filename)))
           (icon (if is-dir
                     (cdr (assoc "direddir" emacs-solo/file-icons))
                   (or (cdr (assoc ext emacs-solo/file-icons))
                       (cdr (assoc "diredfile" emacs-solo/file-icons)))))
           (suffix (cond
                    (is-dir "/")
                    (is-exec "*")
                    (t "")))
           (display-text (propertize
                          (concat icon " " filename suffix)
                          'file-name filename
                          'mouse-face 'highlight
                          'help-echo (concat "Open " filename)
                          'keymap eshell-ls-file-keymap)))
      (cons display-text (cdr file))))


  (defvar eshell-ls-file-keymap
    (let ((map (make-sparse-keymap)))
      (define-key map (kbd "RET") #'eshell-ls-find-file)
      (define-key map (kbd "<return>") #'eshell-ls-find-file)
      (define-key map [mouse-1] #'eshell-ls-find-file)
      (define-key map (kbd "D") #'eshell-ls-delete-file)
      map)
    "Keymap active on Eshell file entries.")

  (defun eshell-ls-file-at-point ()
    "Get the full path of the Eshell listing at point."
    (get-text-property (point) 'file-name))

  (defun eshell-ls-find-file ()
    "Open the Eshell listing at point."
    (interactive)
    (find-file (eshell-ls-file-at-point)))

  (defun eshell-ls-delete-file ()
    "Delete the Eshell listing at point."
    (interactive)
    (let ((file (eshell-ls-file-at-point)))
      (when (yes-or-no-p (format "Delete file %s?" file))
        (delete-file file 'trash))))

  (advice-remove 'eshell-ls-decorated-name #'emacs-solo/eshell-icons)
  (advice-add #'eshell-ls-annotate :filter-return #'emacs-solo/eshell-icons))



(provide 'zt-icons)
